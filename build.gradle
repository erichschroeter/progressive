buildscript {
	repositories {
		maven { url 'http://repo.jfrog.org/artifactory/gradle-plugins'  }
	}
	dependencies {
		classpath group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '2.0.12'
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'artifactory'

group = 'usr.erichschroeter'
version = '0.0.1'
description = 'A library providing classes for tracking progress of tasks.'

sourceCompatibility = 1.6

repositories { mavenCentral() }

dependencies {
	compile group: 'org.swinglabs', name: 'swingx-core', version: '1.6.2-2'
	testCompile group: 'junit', name: 'junit', version: '4.10'
}

task sourceJar(type: Jar) {
	from sourceSets.main.java
	from sourceSets.main.resources
	classifier = 'sources'
}

task javadocJar(type: Jar) {
	dependsOn javadoc
	from javadoc.destinationDir
	classifier = 'javadoc'
}

artifacts {
	archives sourceJar
	archives javadocJar
}

artifactory {
	contextUrl = 'http://localhost:8081/artifactory'
	publish {
		repository {
			repoKey = 'ext-release-local'
			// artifactoryUsername is stored in your gradle.properties
			username = artifactoryUsername
			// artifactoryPassword is stored in your gradle.properties
			password = artifactoryPassword
		}
	}
}

uploadArchives {
	repositories.mavenDeployer {
		pom.project {
			developers {
				developer {
					name 'Erich Schroeter'
					email 'erichschroeter@gmail.com'
					roles {
						role 'author'
						role 'developer'
					}
				}
				scm {
					connection 'git://github.com/erichschroeter/applib.git'
					url 'git://github.com/erichschroeter/applib.git'
				}
			}
		}
	}
}
